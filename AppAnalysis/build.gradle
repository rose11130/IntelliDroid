//使用java插件，定义java项目
apply plugin: 'java'
apply plugin: 'application'

mainClassName = "intellidroid.appanalysis.IntelliDroidAppAnalysis"

//设置编码为UTF-8，防止注释中有中文
tasks.withType(JavaCompile){
        options.encoding = "UTF-8"
}

jar {
    baseName = 'IntelliDroidAppAnalysis'
    version = '0.1.0'
    manifest { attributes 'Main-Class': mainClassName }
}

//目录指向配置
sourceSets {
    main {
        java {
            srcDir 'src/'//指定source目录
        }
    }
}

//通过Maven中央库来检索项目依赖的软件包
repositories {
    mavenCentral()
}

//声明第三方库/依赖项
dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.google.guava', name: 'guava-io', version: 'r03'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
    // runtime files('wala.properties')
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

//表明打包成zip时，要将wala.properties和targetedMethods.txt也放入project.name中，将./android放入project.name + '/android'中
distZip {
    into(project.name) {
        from '.'
        include 'wala.properties'
        include 'targetedMethods.txt'
    }

    into(project.name + '/android') {
        from './android'
    }
}

//task fatJar与artifacts 一起组成了“生成有依赖的jar包”
task fatJar(type: Jar) {
    baseName = project.name + '-all'
    manifest { attributes 'Main-Class': mainClassName }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives fatJar
}

run {
    if (project.hasProperty("appArgs")) {
        if (appArgs.contains("[")) {
            args Eval.me(appArgs)
        } else {
            args appArgs.split()
        }
    }

    //jvmArgs "-Xmx12g", "-Xms10g", "-XX:NewSize=8g", "-XX:NewRatio=2"
}

processTestResources.enabled = false
compileTestJava.enabled = false
testClasses.enabled = false
test.enabled = false

//使用Gradle Wrapper来构建gradle
task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

